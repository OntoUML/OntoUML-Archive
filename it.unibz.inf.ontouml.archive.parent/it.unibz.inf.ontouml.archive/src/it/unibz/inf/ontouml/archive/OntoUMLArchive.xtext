grammar it.unibz.inf.ontouml.archive.OntoUMLArchive with org.eclipse.xtext.common.Terminals

generate ontoUMLArchive "http://www.unibz.it/inf/ontouml/archive/OntoUMLArchive"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	elments+=ModelElement*
;
	
ModelElement:
	( Class | Relationship | GeneralizationSet ) ';'
;

Class:
	(isAbstract?='abstract')? 'class' name=STRING
	stereotype=Stereotype?
;

Relationship:
	Association | Generalization | DependencyLink
;

Association:
	RegularAssociation | NaryAssociation
;

RegularAssociation:
	'association' name=STRING?
	stereotype=Stereotype?
	'from' from=AssociationEnd
	'to' to=AssociationEnd
;

NaryAssociation:
	'association' name=STRING?
	stereotype=Stereotype?
	('end' ends+=AssociationEnd)
	('end' ends+=AssociationEnd)
	('end' ends+=AssociationEnd)+
;

AssociationEnd:
	name=STRING?
	multiplicity=Multiplicity
	':' endType=[Class|STRING]
;

Multiplicity:
	'[' lower=CARDINALITY '..' upper=CARDINALITY ']'
;

CARDINALITY returns ecore::EInt:
	'*' | INT
;

Generalization:
	'gen' name=STRING?
	'super' super=[Class|STRING]
	'sub' sub=[Class|STRING]
;

DependencyLink:
	'dependency' name=STRING?
	'from' from=[Class|STRING]
	'to' to=[Class|STRING]
;

GeneralizationSet:
	'genset' (isDisjoint?='disjoint'? & isComplete?='complete'?) name=STRING?
	generalizations+=Generalization+
;

Stereotype:
	name=STEREOTYPE_STRING
;

terminal STEREOTYPE_STRING returns ecore::EString:
	"<<" ('a'..'z'|'A'..'Z'|' ') ('a'..'z'|'A'..'Z'|' ')* ">>"
;
	